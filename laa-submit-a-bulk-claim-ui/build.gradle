plugins {
    id 'io.freefair.lombok' version '8.14'
    id("com.diffplug.spotless") version "7.2.1"
    id 'au.com.dius.pact' version '4.6.19'
}

apply plugin: 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {

    implementation 'uk.gov.justice.laa.datastewardship.payments:data-claims-api-models:6ec4cb7'

    //Common exception handling
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-application-exception'

    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-restclient'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.reactivestreams:reactive-streams:1.0.4'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    // Source: https://mvnrepository.com/artifact/org.thymeleaf.extras/thymeleaf-extras-springsecurity6
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6:3.1.3.RELEASE'

    implementation 'org.apache.commons:commons-lang3'
    // Front end
    // https://mvnrepository.com/artifact/org.webjars/webjars-locator-lite
    implementation 'org.webjars:webjars-locator-lite'
    // https://mvnrepository.com/artifact/org.webjars.npm/govuk-frontend
    implementation 'org.webjars.npm:govuk-frontend:5.13.0'
    implementation 'org.webjars.npm:ministryofjustice__frontend:5.1.5'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.prometheus:prometheus-metrics-tracer-initializer'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-restclient-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-cache-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client-test'

    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.springframework.security:spring-security-test'

    testImplementation 'org.testcontainers:mockserver:1.21.3'
    testImplementation 'org.mock-server:mockserver-client-java:5.15.0'

    testImplementation 'au.com.dius.pact.consumer:junit5:4.6.19'
}

// To keep pact stuff seperate
sourceSets {
    pactTest {
        java.srcDir file('src/pactTest/java')
        resources.srcDir file('src/pactTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    }
}

configurations {
    pactTestImplementation.extendsFrom testImplementation
    pactTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('pactTest', Test) {
    description = 'Runs the pact tests.'
    group = 'verification'
    testClassesDirs = sourceSets.pactTest.output.classesDirs
    classpath = sourceSets.pactTest.runtimeClasspath
    useJUnitPlatform()
    // Resolve the directory path to a String for the system property
    systemProperty 'pact.rootDir', layout.buildDirectory.dir("pacts").get().asFile.path
}

tasks.named('processPactTestResources') {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}


test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/BulkUploadApplication.class', '**/*Config.class', '**/*Dto.class'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/BulkUploadApplication.class', '**/*Config.class', '**/*Dto.class'])
        }))
    }

    violationRules {
        /*CwaUploadService fails this check, but due to how the tests have been written. This class
            will be removed in the future so for the time being I have disabled this until the
            codebase has been fleshed out.*/
        failOnViolation = false

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                minimum = 0.75
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat()
    }
}


pact {
    publish {
        pactBrokerUrl = 'http://localhost:9292'
        
        pactDirectory = layout.buildDirectory.dir("pacts").get().asFile.path
        
        consumerVersion = project.version != 'unspecified' ? project.version : '1.0.0'
        tags = ['dev', 'local']
    }
}


tasks.compileJava {
    options.compilerArgs.addAll(['-Xlint:deprecation', '-Xlint:unchecked'])
}