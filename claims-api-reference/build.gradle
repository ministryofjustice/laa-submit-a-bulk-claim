plugins {
    id 'org.openapi.generator' version '7.14.0'
}

apply plugin: 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'

repositories {
    mavenCentral()
}

dependencies {

    /**
     * Upgrades to resolve vulnerabilities
     */
    // Upgrade to fix Uncontrolled Recursion vulnerability in commons-lang3
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    // Update to fix CVE-2025-48989
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.10'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
}

sourceSets.main.java.srcDirs += ['generated/src/main/java']

checkstyleMain.exclude "*"

openApiGenerate {
    generatorName = "spring"
    // https://github.com/ministryofjustice/laa-data-claims-api/blob/openapi/v0/claims-data/api/open-api-specification.yml
    inputSpec = "$rootDir/claims-api-reference/open-api-specification.yml".toString()
    outputDir = "$rootDir/claims-api-reference/generated".toString()
    apiPackage = "uk.gov.justice.laa.claims.api"
    invokerPackage = "uk.gov.justice.laa.claims.invoker"
    modelPackage = "uk.gov.justice.laa.claims.model"
    configOptions = [
            generateBuilders    : "true",
            interfaceOnly       : "true", // This will only generate interfaces, not implementations
            serializableModel   : "true",
            skipDefaultInterface: "true",
            useJakartaEe        : "true",
            useSpringBoot3      : "true",
            useTags             : "true",
            openApiNullable     : "false",
    ]
}

compileJava.dependsOn 'openApiGenerate'

clean {
    delete "$rootDir/claims-api-reference/generated"
}

// disable for overall project when running a build
tasks.named('bootJar') {
    enabled = false
}

tasks.named('bootRun') {
    enabled = false
}
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}